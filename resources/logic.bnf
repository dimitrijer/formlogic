WellFormedFormula = QuantifiedFormula | Disjunction
QuantifiedFormula = SingleQuantifier QuantifiedFormula |
		    SingleQuantifier <'{'> QuantifiedFormula <'}'> |
		    SingleQuantifier <'{'> Disjunction <'}'>
<SingleQuantifier> = Quantifier | QuantifierNegation
Quantifier = FOREACH LITERAL | EXISTS LITERAL
QuantifierNegation = <'~'> Quantifier
Disjunction = Disjunction <'||'> Conjunction | Conjunction
Conjunction = Conjunction <'&&'> Implication | Implication
Implication = Implication <'=>'> Term | Term
Negation = <'~'> Term
Term = <'('> WellFormedFormula <')'> | Negation | Predicate | LITERAL
Predicate = PRED <'('> Arguments <')'>
<Arguments> = Arguments <','> Argument | Argument
Argument = LITERAL | Predicate

LITERAL = #'[a-z]+'
PRED = #'[A-Z][a-z]*'
FOREACH = <#'\\A'>
EXISTS = <#'\\E'>
